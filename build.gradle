plugins {
	id 'java-library-distribution'
  	id 'jacoco'
  	id 'maven'
}

group = 'com.github.CST-Group'

description = "The Multipurpose Enhanced Cognitive Architecture (MECA)"

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.4.1'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { 
    	url "https://github.com/rosjava/rosjava_mvn_repo/raw/master"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/release"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/external"
    }
}

dependencies {
   api 'com.github.CST-Group:cst:0.6.0'
   testImplementation group: 'junit', name: 'junit', version: '4.12'
   testImplementation 'org.ros.rosjava_messages:std_msgs:0.5.11'
   testImplementation 'org.ros.rosjava_messages:rosjava_test_msgs:0.3.0'
}

jar {
    manifest {
      attributes(
        'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')    
      )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task uberJar(type: Jar) {
    archiveClassifier = 'full'
    manifest {
      attributes(
        'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')    
      )
      
    }
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
artifacts
{
    archives javadocJar, sourcesJar, uberJar
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
